ORG 0000H                                       ;STARTING THE PROGRAM FROM ORIGIN ADDRESS(0000H)
               MOV P2,#0XFF                     ;TURN OFF THE SEVEN SEG. DISPLAY
START:         MOV P0,#0XF0                     ;ROW AS INPUT WITH 1111 & COLUMN AS OUTPUT WITH 0000
L1:            MOV A,P0                         ;READ PORT 0
               CJNE A,#0XF0,KEY_PRESSED         ;IF ANY KEY IS PRESSED (NOT = F0) GO TO KEY_PRESSED, ELSE GO TO NEXT LINE
               SJMP L1
KEY_PRESSED:   MOV 30H,A                        ;STORE ROW INFORMATION TO 30H
               MOV P0,#0X0F                     ;COLUMN AS INPUT WITH 1111 & ROW AS OUTPUT WITH 0000
               MOV A,P0                         ;READ PORT 0
               ORL A,30H                        ;OR OPERATION WITH THE CONTENT OF DIRECT ADDRESS (30H) WITH THE ACCUMULATOR CONTENT
               MOV 31H,A                        ;STORE THE ACCUMULATOR CONTENT TO THE DIRECT ADDRESS(31H)
               MOV DPTR,#0100H                  ;THE IMMEDIATE DATA(#0100H) IS MOVED TO THE DATA POINTER REGISTER(DPTR)
REPEAT:        CLR A                            ;ACCUMULATOR CONTENT IS CLEARED(00000000)
               MOVC A,@A+DPTR                   ;THE DATAPOINTER VALUE GETS ADDED WITH ACCUMULATOR POINTED ADDRESS
               CJNE A,31H,NEXT_BYTE             ;COMPARE AND JUMP TO NEXT_BYTE LABEL IF ACCUMULATOR CONTENT IS NOT EQUAL TO THE DIRECT ADDRESS(31H)
               SJMP DISPLAY                     ;JUMP TO DISPLAY LABEL
NEXT_BYTE:     INC DPTR                         ;THE DPTR CONTENT WILL BE INCREMENTED BY 1
               SJMP REPEAT                      ;JUMP TO REPEAT LABEL

DISPLAY:       MOV A,DPL                        ;STORING THE DPL CONTENT TO THE ACCUMULATOR REGISTER
               MOV DPTR,#0120H                  ;STORING THE DATA(#0120H) TO THE DATAPOINTER(DPTR)
               MOVC A,@A+DPTR                   ;THE DATAPOINTER GETS ADDED WITH THE CONTENT OF POINTED ADDRESS BY ACCUMULATOR
               MOV P2,A                         ;STORING THE ACCUMULATOR CONTENT TO PORT 2
               SJMP START                       ;JUMP TO START LABEL

ORG 0100H                                       ;CREATING THE ARRAY FOR KEYPAD  
DB 0X77,0X7B,0X7D,0X7E,0XB7,0XBB,0XBD,0XBE,0XD7,0XDB,0XDD,0XDE,0XE7,0XEB,0XED,0XEE

ORG 0120H                                       ;CREATING THE ARRAY FOR SEVEN SEGMENT DISPLAY
DB 0XF9,0XA4,0XB0,0X88,0X99,0X92,0X82,0X83,0XF8,0X80,0X90,0XC6,0X00,0XC0,0XFF,0XA1
END                                             ;END THE PROGRAM
